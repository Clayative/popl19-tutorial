-- Agda definition of C-- abstract syntax trees
-- and Haskell bindings to the ones generated by the parser.

module AST where

open import Library

{-# FOREIGN GHC import qualified Data.Text #-}
{-# FOREIGN GHC import While.Abs #-}
{-# FOREIGN GHC import While.Print #-}

data Id : Set where
  mkId : List Char → Id

{-# COMPILE GHC Id = data Ident (Ident) #-}

data Type : Set where
  bool int : Type

{-# COMPILE GHC Type = data Type
  ( TBool
  | TInt
  ) #-}

data Boolean : Set where
  true false : Boolean

{-# COMPILE GHC Boolean = data Boolean (BTrue | BFalse) #-}

data Exp : Set where
  eId       : (x : Id)                 → Exp
  eInt      : (i : ℤ)                  → Exp
  eBool     : (b : Boolean)            → Exp
  eNot      : (e : Exp)                → Exp
  ePlus     : (e e' : Exp)             → Exp
  eMinus    : (e e' : Exp)             → Exp
  eGt       : (e e' : Exp)             → Exp
  eAnd      : (e e' : Exp)             → Exp


{-# COMPILE GHC Exp = data Exp
  ( EId
  | EInt
  | EBool
  | ENot
  | EPlus
  | EMinus
  | EGt
  | EAnd
  ) #-}

data Stm : Set where
  sInit   : (t : Type) (x : Id) (e : Exp) → Stm
  sAss    : (x : Id) (e : Exp)            → Stm
  sWhile  : (e : Exp) (ss : List Stm)     → Stm
  sIfElse : (e : Exp) (ss ss' : List Stm) → Stm

{-# COMPILE GHC Stm = data Stm
  ( SInit
  | SAss
  | SWhile
  | SIfElse
  ) #-}

data Program : Set where
  program : (ss : List Stm) (e : Exp) → Program

{-# COMPILE GHC Program = data Program (Prg) #-}

-- Pretty printer

printId  : Id → String
printId (mkId s) = String.fromList s

postulate
  printType    : Type    → String
  printExp     : Exp     → String
  printStm     : Stm     → String
  printProgram : Program → String

{-# COMPILE GHC printType    = \ t -> Data.Text.pack (printTree (t :: Type)) #-}
{-# COMPILE GHC printExp     = \ e -> Data.Text.pack (printTree (e :: Exp))  #-}
{-# COMPILE GHC printStm     = \ s -> Data.Text.pack (printTree (s :: Stm))  #-}
{-# COMPILE GHC printProgram = \ p -> Data.Text.pack (printTree (p :: Program)) #-}
